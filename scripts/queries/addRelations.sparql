PREFIX sari: <https://platform.swissartresearch.net/>
PREFIX search: <https://platform.swissartresearch.net/search/>
PREFIX crmdig: <http://www.ics.forth.gr/isl/CRMdig/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX gndo: <https://d-nb.info/standards/elementset/gnd#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX frbroo: <http://iflastandards.info/ns/fr/frbr/frbroo/>
PREFIX rds: <http://schema.swissartresearch.net/ontology/rds#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX schema: <http://schema.org/>

# Clear graph
DROP GRAPH sari:relations;

# Add basic ontology for Search relations
INSERT DATA {
  GRAPH sari:relations {
    search:Entity a rdfs:Class ;
      rdfs:equivalentClass crm:E1_CRM_Entity ;
      rdfs:label 'Entity'@en, 'Entität'@de, 'Entité'@fr, 'Entità'@it .
    search:Collection rdfs:label 'Collection'@en, 'Sammlung'@de, 'Collection'@fr, 'Collezione'@it ;
      a rdfs:Class ;
      rdfs:subClassOf search:Entity ;
      rdfs:equivalentClass crm:E78_Curated_Holding .
    search:Object rdfs:label 'Object'@en, 'Objekt'@de, 'Objet'@fr, 'Oggetto'@it ;
      a rdfs:Class ;
      rdfs:subClassOf search:Entity ;
      rdfs:equivalentClass crm:E22_Human-Made_Object .
    search:Person rdfs:label 'Person'@en, 'Person'@de, 'Personne'@fr, 'Persona'@it ;
      a rdfs:Class ;
      rdfs:subClassOf search:Entity ;
      rdfs:equivalentClass crm:E21_Person, rds:Person .
    search:Actor rdfs:label 'Actor/Legal Entity'@en, 'Körperschaft'@de, 'Entité juridique'@fr, 'Entità giuridica'@it ;
      a rdfs:Class ;
      rdfs:subClassOf search:Entity ;
      rdfs:equivalentClass crm:E39_Actor  .
    search:Place rdfs:label 'Place'@en, 'Ort'@de, 'Lieu'@fr, 'Luogo'@it  ;
      a rdfs:Class ;
      rdfs:subClassOf search:Entity ;
      rdfs:equivalentClass crm:E53_Place, rds:Place  .
    search:Type rdfs:label 'Type'@en, 'Typ'@de, 'Type'@fr, 'Tipo'@it ;
      a rdfs:Class ;
      rdfs:subClassOf search:Entity ;
      rdfs:equivalentClass crm:E55_Type, rds:Type .
    search:Event rdfs:label 'Event'@en, 'Ereignis'@de, 'Événement'@fr, 'Evento'@it ;
      a rdfs:Class ;
      rdfs:subClassOf search:Entity ;
      rdfs:equivalentClass crm:E5_Event, rds:Event .
    search:BibliographicItem rdfs:label 'Bibliographic Item'@en, 'Bibliographischer Eintrag'@de, 'Entrée bibliographique'@fr, 'Voce bibliografica'@it ;
      a rdfs:Class ;
      rdfs:subClassOf search:Entity ;
      rdfs:equivalentClass frbroo:F24_Publication_Expression, rds:BibliographicItem .
  }
};

# Add Search entity types to respective resources
INSERT {
  GRAPH sari:relations {
      ?subject a ?searchType .
  }
} WHERE {
  ?subject a ?crmType .
  VALUES(?crmType ?searchType) {
    (crm:E21_Person search:Person)
    (crm:E39_Actor search:Actor)
    (crm:E55_Type search:Type)
    (crm:E53_Place search:Place)
    (crm:E78_Curated_Holding search:Collection)
    (crm:E22_Human-Made_Object search:Object)
    (crm:E5_Event search:Event)
  }
  FILTER(REGEX(STR(?subject), 'swissartresearch.net'))
};

# Add CIDOC-CRM types for external entities
INSERT {
  GRAPH sari:relations {
    ?subject a crm:E53_Place .
  }
} WHERE {
  ?person a gndo:Person ;
    ?placeRelation ?subject .
  VALUES (?placeRelation) {
    (gndo:placeOfActivity)
    (gndo:placeOfBirth)
    (gndo:placeOfDeath)
    (gndo:geographicAreaCode)
  }
};

INSERT {
  GRAPH sari:relations {
    ?subject a crm:E21_Person .
  }
} WHERE {
  ?person a gndo:Person ;
    ?personRelation ?subject .
  VALUES (?placeRelation) {
    (gndo:familialRelationship)
  }
};

# Materialise triples defined through E13 Attribute Assignments that are not present yet in the graph
INSERT {
  GRAPH sari:relations {
    ?subject ?property ?object .
    }
} WHERE {
  ?assignment a crm:E13_Attribute_Assignment ;
              crm:P140_assigned_attribute_to ?subject ;
              crm:P141_assigned ?object ;
              crm:P177_assigned_property_of_type ?property .
  FILTER NOT EXISTS {
    ?subject ?property ?object
  }
};

# Add inverse relations
INSERT {
  GRAPH <https://resource.swissartresearch.net/graph/network> {
    ?object ?inversePredicate ?subject .
  }
} WHERE {
  GRAPH <https://resource.swissartresearch.net/graph/network> {
    ?subject ?predicate ?object .
  }
  ?inversePredicate owl:inverseOf ?predicate .
}